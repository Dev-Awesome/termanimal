💭 Handle pet needs across time
🐇⌚️🍇

	🐇❗📯 pet 🐶 🍇
		💭 Compute love
		🕰🐇💻❗ ➡️ current
		current ➖ 🕰 pet ❗ ➡️ delta
		💭 20 hours later, update love
		↪️ delta ▶️ 72000 🍇

			💭 Update data
			🍕 pet ❗️ ➡️ fullness
			🥏 pet ❗️ ➡️ fun

			↪️ fullness 🙌 4 🤝 fun 🙌 4 🍇
				💭 +1 ❤️ on fullness and fun
				💖 pet ❗️ ➡️ 🖍🆕 love

				💭 Cap to 4
				↪️ love ◀️ 4 🍇
					love ➕ 1 ➡️ 🖍love
					💘 pet love ❗️
				🍉
			🍉
			🙅↪️ fullness 🙌 0 🤝 fun 🙌 0 🍇
				💭 -1 ❤️ on hunger and bored
				💖 pet ❗️ ➡️ 🖍🆕 love

				💭 Cap to 0
				↪️ love ▶️ 0 🍇
					love ➖ 1 ➡️ 🖍love
					💘 pet love ❗️
				🍉
			🍉

			🍔 pet 0 ❗️
			🎾 pet 0 ❗️
			⏰ pet current❗
			🎥🐇📀 pet❗️
		🍉

		💭 Update state
		🆕🎰🆕❗️ ➡️ generator

		💭 Decrease food and fun with 1/15
		🔢 generator 1 15 ❗️ ➡️ alea
		↪️ alea 🙌 1 🍇

			💭 Decrease hunger gauge
			🍕 pet ❗️ ➡️ 🖍🆕 fullness
			↪️ fullness ▶️ 0 🍇
				fullness ➖ 1 ➡️ 🖍fullness

				💭 Update pet info
				🍔 pet fullness ❗️
				🎥🐇📀 pet❗️
			🍉

			💭 Decrease fun gauge
			🥏 pet ❗️ ➡️ 🖍🆕 fun
			↪️ fun ▶️ 0 🍇
				fun ➖ 1 ➡️ 🖍fun

				💭 Update pet info
				🎾 pet fun ❗️
				🎥🐇📀 pet❗️
			🍉
		🍉

		💭 Set sick with 1/50

		💭 Set dirty with 1/50

	🍉
🍉
