🐇🐶🍇

	🖍🆕 species 🔡

	🖍🆕 love 🔢

	🖍🆕 fullness 🔢

	🖍🆕 fun 🔢

	🖍🆕 last_day_of_love 🔢

	🆕 🍼 species 🔡 🍼 love 🔢 🍼 fullness 🔢 🍼 fun 🔢 🍼 last_day_of_love 🔢 🍇🍉

	🆕🥚 🍼 species 🔡 🍇
		0 ➡️🖍 love
		2 ➡️🖍 fullness
		2 ➡️🖍 fun
		🕰🐇💻❗ ➡️🖍 last_day_of_love
	🍉

	💭 Serialize pet data
	❗📇 ➡️ 📇🍇

		💭 Make a list of all string properties
		🆕🍨🐚🔡🍆🐸❗️ ➡️ info

		🐻 info species ❗️
		🐻 info 🔡 love 10 ❗️❗️
		🐻 info 🔡 fullness 10 ❗️❗️
		🐻 info 🔡 fun 10 ❗️❗️
		🐻 info 🔡 last_day_of_love 10 ❗️❗️

		💭 Join it by adding new line
		🆕🔡🍨 info 🔤❌n🔤❗️ ➡️ stringify

		💭 Binary out of string
		↩️ 📇 stringify ❗️
	🍉

	💭 Deserialize pet data
	🐇❗🖨 data 📇 ➡️ 🍬🐶🍇
		🍺🔡 data ❗️ ➡️ content

		💭 Split by new line
		🔫 content 🔤❌n🔤 ❗️➡️ lines

		💭 Check for invalid pet file
		↪️❎ 5 🙌 🐔 lines ❗️❗️🍇
			↩️🤷‍♂️
		🍉

		💭 Retrieve info
		🐽 lines 0 ❗️➡️ species
		🐽 lines 1 ❗️➡️ love_str
		🐽 lines 2 ❗️➡️ fullness_str
		🐽 lines 3 ❗️➡️ fun_str
		🐽 lines 4 ❗️➡️ last_day_of_love_str

		💭 Handle parsing
		🔢 love_str 10❗️➡️ love
		🔢 fullness_str 10❗️➡️ fullness
		🔢 fun_str 10❗️➡️ fun
		🔢 last_day_of_love_str 10❗️➡️ last_day_of_love

		↪️🤷‍♂️🙌 love 🍇↩️🤷‍♂️🍉
		↪️🤷‍♂️🙌 fullness 🍇↩️🤷‍♂️🍉
		↪️🤷‍♂️🙌 fun 🍇↩️🤷‍♂️🍉
		↪️🤷‍♂️🙌 last_day_of_love 🍇↩️🤷‍♂️🍉

		🆕🐶🆕 species 🍺love 🍺fullness 🍺fun 🍺last_day_of_love ❗️➡️ pet
		↩️ pet
	🍉

	💭 Getters

	❗🧬 ➡️ 🔡🍇
		↩️ species
	🍉

	❗💖➡️🔢🍇
		↩️ love
	🍉

	❗🍕➡️🔢🍇
		↩️ fullness
	🍉

	❗🥏➡️🔢🍇
		↩️ fun
	🍉

	❗🕰➡️🔢🍇
		↩️ last_day_of_love
	🍉


	💭 Setters

	❗💘 value 🔢🍇
		value ➡️ 🖍love
	🍉

	❗🍔 value 🔢🍇
		value ➡️ 🖍fullness
	🍉

	❗🎾 value 🔢🍇
		value ➡️ 🖍fun
	🍉

	❗⏰ value 🔢🍇
		value ➡️ 🖍last_day_of_love
	🍉
🍉
