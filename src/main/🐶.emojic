🐇🐶🍇

	🖍🆕 species 🔡

	🖍🆕 fullness 🔢

	🆕 🍼 species 🔡 🍼 fullness 🔢 🍇🍉

	🆕🥚 🍼 species 🔡 🍇
		0 ➡️🖍 fullness
	🍉

	💭 Serialize pet data
	❗📇 ➡️ 📇🍇

		💭 Make a list of all string properties
		🆕🍨🐚🔡🍆🐸❗️ ➡️ info

		🐻 info species ❗️
		🐻 info 🔡 fullness 10 ❗️❗️

		💭 Join it by adding new line
		🆕🔡🍨 info 🔤❌n🔤❗️ ➡️ stringify

		💭 Binary out of string
		↩️ 📇 stringify ❗️
	🍉

	💭 Deserialize pet data
	🐇❗🖨 data 📇 ➡️ 🍬🐶🍇
		🍺🔡 data ❗️ ➡️ content

		💭 Split by new line
		🔫 content 🔤❌n🔤 ❗️➡️ lines

		💭 Check for invalid pet file
		↪️❎ 2 🙌 🐔 lines ❗️❗️🍇
			↩️🤷‍♂️
		🍉

		💭 Retrieve info
		🐽 lines 0 ❗️➡️ species
		🐽 lines 1 ❗️➡️ fullness_str

		💭 Handle parsing
		🔢 fullness_str 10❗️➡️ fullness

		↪️🤷‍♂️🙌 fullness 🍇
			↩️🤷‍♂️
		🍉

		🆕🐶🆕 species 🍺fullness ❗️➡️ pet
		↩️ pet
	🍉

	💭 Getters

	❗🧬 ➡️ 🔡🍇
		↩️ species
	🍉

	❗🍕➡️🔢🍇
		↩️ fullness
	🍉


	❗🍔 value 🔢🍇
		value ➡️ 🖍fullness
	🍉
🍉
