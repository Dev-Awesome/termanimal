🐇🐶🍇

	🖍🆕 species 🔡

	🖍🆕 fullness 🔢

	🖍🆕 fun 🔢

	🆕 🍼 species 🔡 🍼 fullness 🔢 🍼 fun 🔢 🍇🍉

	🆕🥚 🍼 species 🔡 🍇
		0 ➡️🖍 fullness
		0 ➡️🖍 fun
	🍉

	💭 Serialize pet data
	❗📇 ➡️ 📇🍇

		💭 Make a list of all string properties
		🆕🍨🐚🔡🍆🐸❗️ ➡️ info

		🐻 info species ❗️
		🐻 info 🔡 fullness 10 ❗️❗️
		🐻 info 🔡 fun 10 ❗️❗️

		💭 Join it by adding new line
		🆕🔡🍨 info 🔤❌n🔤❗️ ➡️ stringify

		💭 Binary out of string
		↩️ 📇 stringify ❗️
	🍉

	💭 Deserialize pet data
	🐇❗🖨 data 📇 ➡️ 🍬🐶🍇
		🍺🔡 data ❗️ ➡️ content

		💭 Split by new line
		🔫 content 🔤❌n🔤 ❗️➡️ lines

		💭 Check for invalid pet file
		↪️❎ 3 🙌 🐔 lines ❗️❗️🍇
			↩️🤷‍♂️
		🍉

		💭 Retrieve info
		🐽 lines 0 ❗️➡️ species
		🐽 lines 1 ❗️➡️ fullness_str
		🐽 lines 2 ❗️➡️ fun_str

		💭 Handle parsing
		🔢 fullness_str 10❗️➡️ fullness
		🔢 fun_str 10❗️➡️ fun

		↪️🤷‍♂️🙌 fullness 🍇↩️🤷‍♂️🍉
		↪️🤷‍♂️🙌 fun 🍇↩️🤷‍♂️🍉

		🆕🐶🆕 species 🍺fullness 🍺fun ❗️➡️ pet
		↩️ pet
	🍉

	💭 Getters

	❗🧬 ➡️ 🔡🍇
		↩️ species
	🍉

	❗🍕➡️🔢🍇
		↩️ fullness
	🍉

	❗🥏➡️🔢🍇
		↩️ fun
	🍉


	💭 Setters

	❗🍔 value 🔢🍇
		value ➡️ 🖍fullness
	🍉

	❗🎾 value 🔢🍇
		value ➡️ 🖍fun
	🍉
🍉
