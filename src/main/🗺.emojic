🐇🗺🍇

	💭 Feed process
	🐇❗🍕 food 🔡 🍇

		↪️ 📽🐇📀❗️➡️ pet 🍇

			💭 Increase fullness gauge
			🍕 pet ❗️ ➡️ 🖍🆕 fullness

			💭 Cap to 4
			↪️ fullness ◀️ 4 🍇
				fullness ➕ 1 ➡️ 🖍fullness

				💭 Update pet info
				🍔 pet fullness ❗️
				🎥🐇📀 pet❗️

				💭 Feedback
				😀 🍪 🧬pet❗️ 🔤 ate 🔤 food 🍪 ❗️
			🍉
			🙅🍇
				💭 Not hungry
				😀 🍪 🧬pet❗️ 🔤 is not hungry🔤 🍪 ❗️
			🍉
		🍉
	🍉

	💭 Activity process
	🐇❗🥏 activity 🔡🍇

		↪️ 📽🐇📀❗️➡️ pet 🍇

			💭 Increase fun gauge
			🥏 pet ❗️ ➡️ 🖍🆕 fun

			💭 Cap to 4
			↪️ fun ◀️ 4 🍇
				fun ➕ 1 ➡️ 🖍fun

				💭 Update pet info
				🎾 pet fun ❗️
				🎥🐇📀 pet❗️

				💭 Feedback
				😀 🍪 🧬pet❗️ 🔤 played 🔤 activity 🍪 ❗️
			🍉
			🙅🍇
				💭 Overexited
				😀 🍪 🧬pet❗️ 🔤 growls🔤 🍪 ❗️
			🍉
		🍉
	🍉

	💭 Cure process
	🐇❗💊 activity 🔡🍇

		↪️ 📽🐇📀❗️➡️ pet 🍇

			💭 Get sick status
			🌡 pet ❗️ ➡️ sick

			↪️ sick 🙌 1 🍇

				💭 Update pet info
				💊 pet 0 ❗️
				🎥🐇📀 pet❗️

				💭 Feedback
				😀 🍪 🧬pet❗️ 🔤 is cured🔤 🍪 ❗️
			🍉
			🙅🍇
				💭 Not sick
				😀 🍪 🧬pet❗️ 🔤 is not sick🔤 🍪 ❗️
			🍉
		🍉
	🍉

	💭 Cure process
	🐇❗🧼 activity 🔡🍇

		↪️ 📽🐇📀❗️➡️ pet 🍇

			💭 Get dirty status
			🍂 pet ❗️ ➡️ dirty

			↪️ dirty 🙌 1 🍇

				💭 Update pet info
				🧼 pet 0 ❗️
				🎥🐇📀 pet❗️

				💭 Feedback
				😀 🍪 🧬pet❗️ 🔤 is cleaned🔤 🍪 ❗️
			🍉
			🙅🍇
				💭 Not dirty
				😀 🍪 🧬pet❗️ 🔤 is not dirty🔤 🍪 ❗️
			🍉
		🍉
	🍉

	💭 Status process
	🐇❗📊🍇

		↪️ 📽🐇📀❗️➡️ pet 🍇

			👄🧬 pet ❗️❗️
			↪️ 🌡 pet ❗️ 🙌 1 🍇
				👄🔤 🌡🔤❗️
			🍉
			↪️ 🍂 pet ❗️ 🙌 1 🍇
				👄🔤 🍂🔤❗️
			🍉
			😀🔤🔤❗️

			👄 🔤❤️ 🔤❗️
			🔂 i 🆕⏩⏩ 0 💖 pet ❗️❗️ 🍇
				👄 🔤❤️ 🔤❗️
			🍉
			🔂 i 🆕⏩⏩ 💖 pet ❗️ 4❗️ 🍇
				👄 🔤▫️ 🔤❗️
			🍉
			😀🔤🔤❗️

			👄 🔤🍖 🔤❗️
			🔂 i 🆕⏩⏩ 0 🍕 pet ❗️❗️ 🍇
				👄 🔤🍖 🔤❗️
			🍉
			🔂 i 🆕⏩⏩ 🍕 pet ❗️ 4❗️ 🍇
				👄 🔤▫️ 🔤❗️
			🍉
			😀🔤🔤❗️

			👄 🔤🥏 🔤❗️
			🔂 i 🆕⏩⏩ 0 🥏 pet ❗️❗️ 🍇
				👄 🔤🥏 🔤❗️
			🍉
			🔂 i 🆕⏩⏩ 🥏 pet ❗️ 4❗️ 🍇
				👄 🔤▫️ 🔤❗️
			🍉
			😀🔤🔤❗️
		🍉
	🍉

	💭 Adoption process
	🐇❗🏡🍇

		😀 🔤Welcome to our animal shelter !🔤❗️

		😀 🔤What kind of pet do you want ?🔤❗️
		👄 🔤[emoji]: 🔤❗️

		💭 Wait for species
		🆕🔡👂🏼❗️ ➡️ input

		💭 Get only first grapheme
		🔪 input 0 1❗️ ➡️ species

		↪️ 🐶🐕 species❓ 🍇
			😀 🍪🔤You're in luck ! We just found a 🔤 species 🔤 waiting for some love from a new family.🔤 🍪❗️
			😀 🔤Adoption process done, enjoy your new life with your new bestfriend !🔤❗️

			💭 Build pet
			🆕🐶🥚 species❗️ ➡️ pet

			💭 Save it
			🎥🐇📀 pet❗️
		🍉
		🙅🍇
			😀 🍪🔤Sorry but we do not have any `🔤 species 🔤` here.🔤 🍪❗️
		🍉
	🍉

	💭 Check if a grapheme is animal emoji
	🐇❓🐶 str 🔡 ➡️ 👌 🍇
		↩️ ❎ 🤷‍♂️ 🙌  🔍 🐶🐇😀❗️ str❗️❗️
	🍉
🍉

