🐇🗺🍇

	💭 Feed process
	🐇❗🍕 food 🔡 🍇

		↪️ 📽🐇📀❗️➡️ pet 🍇

			💭 Increase fullness gauge
			🍕 pet ❗️ ➡️ 🖍🆕 fullness

			💭 Cap to 4
			↪️ fullness ◀️ 4 🍇
				fullness ➕ 1 ➡️ 🖍fullness

				💭 Update pet info
				🍔 pet fullness ❗️
				🎥🐇📀 pet❗️

				💭 Feedback
				😀 🍪 🧬pet❗️ 🔤 ate 🔤 food 🍪 ❗️
			🍉
			🙅🍇
				💭 Not hungry
				😀 🍪 🧬pet❗️ 🔤 is not hungry🔤 🍪 ❗️
			🍉
		🍉
	🍉

	💭 Activity process
	🐇❗🥏 activity 🔡🍇

		↪️ 📽🐇📀❗️➡️ pet 🍇

			💭 Increase fun gauge
			🥏 pet ❗️ ➡️ 🖍🆕 fun

			💭 Cap to 4
			↪️ fun ◀️ 4 🍇
				fun ➕ 1 ➡️ 🖍fun

				💭 Update pet info
				🎾 pet fun ❗️
				🎥🐇📀 pet❗️

				💭 Feedback
				😀 🍪 🧬pet❗️ 🔤 played 🔤 activity 🍪 ❗️
			🍉
			🙅🍇
				💭 Overexited
				😀 🍪 🧬pet❗️ 🔤 growls🔤 🍪 ❗️
			🍉
		🍉
	🍉

	💭 Status process
	🐇❗📊🍇

		↪️ 📽🐇📀❗️➡️ pet 🍇

			😀🧬 pet ❗️❗️
			😀 🍪 🔤🍖 🔤 🔡🍕 pet ❗️ 10 ❗️ 🍪❗️
			😀 🍪 🔤🥏 🔤 🔡🥏 pet ❗️ 10 ❗️ 🍪❗️
		🍉
	🍉

	💭 Adoption process
	🐇❗🏡🍇

		😀 🔤Welcome to our animal shelter !🔤❗️
		💭😀 🔤We heard you wanted to 🔤❗️

		💭 Wait for species
		🆕🔡👂🏼❗️ ➡️ input

		💭 Get only first grapheme
		🔪 input 0 1❗️ ➡️ species

		😀 🍪🔤You're in luck ! We just found a 🔤 species 🔤 waiting for some love from a new family.🔤 🍪❗️

		💭 Build pet
		🆕🐶🥚 species❗️ ➡️ pet

		💭 Save it
		🎥🐇📀 pet❗️

		😀 🍪🔤You just adopted a beautiful 🔤 species 🍪❗️
	🍉
🍉
