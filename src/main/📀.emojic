💭 Handle I/O
💭 Pet file is ~/.termanimal/pet
🐇📀🍇

	💭 Store pet data
	🐇❗️🎥 pet 🐶 🍇

		💭 Build ~/.termanimal (do nothing if exists)
		🌳🐇💻  🔤HOME🔤❗️ ➡️ home
		🍪🍺 home 🔤/.termanimal🔤🍪 ➡️ config_folder
		📁🕊📑 config_folder❗️

		💭 Compute pet's file
		🍪config_folder 🔤/pet🔤🍪 ➡️ pet_file

		💭 Write data
		📻🐇📄 pet_file 📇 pet ❗️ ❗️
	🍉

	💭 Remove pet data
	🐇❗️🗑🍇

		🌳🐇💻  🔤HOME🔤❗️ ➡️ home
		🍪🍺 home 🔤/.termanimal/pet🔤🍪 ➡️ pet_file

		💭 Delete file
		🔫🕊📑 pet_file ❗️
	🍉

	💭 Check if pet data exists
	🐇❗️🤔 ➡️ 👌 🍇

		🌳🐇💻  🔤HOME🔤❗️ ➡️ home
		🍪🍺 home 🔤/.termanimal/pet🔤🍪 ➡️ pet_file

		💭 Check file exists and is readable and writeable
		↩️ 📜🕊📑 pet_file ❗️ 🤝 📝🕊📑 pet_file ❗️
	🍉

	💭 Load pet data
	🐇❗️📽 ➡️ 🍬🐶 🍇

		🌳🐇💻  🔤HOME🔤❗️ ➡️ home
		🍪🍺 home 🔤/.termanimal/pet🔤🍪 ➡️ pet_file

		💭 Read pet info from file
		🥑 data 📇🐇📄 pet_file ❗️ 🍇
			🖨🐇🐶 data ❗ ➡️ pet
			↩️ pet
		🍉
		🙅‍♀️ error 🍇
			💭 File may not exists, or pet info have been corrupted
		🍉

		↩️ 🤷‍♂️
	🍉
🍉
