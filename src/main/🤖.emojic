📜 🔤😀.emojic🔤
📜 🔤📀.emojic🔤
📜 🔤🐶.emojic🔤
📜 🔤🗺.emojic🔤
📜 🔤⌚️.emojic🔤

💭 Parse command line arguments
🐇🤖🍇

	💭 Store command line `action` (first parameter)
	🖍🆕 action 🍬🔡

	🆕 🍇🍉

	💭 Parse command line arguments (filtered program name)
	💭 Cannot be run in initializer (bug)
	❗️🎛 args 🍨🐚🔡🍆 🍇

		↪️ 🐔 args ❗️ 🙌 1 🍇
			🐽 args 0 ❗️ ➡️ 🖍action
		🍉
	🍉

	💭 Act in function of action
	❗️🎬 🍇

		↪️ 🤔🐇📀❗️ 🍇

			💭 Pet file exists
			↪️ 📽🐇📀❗️➡️ pet 🍇

				💭 First of all, update pet status
				📯🐇⌚️ pet ❗️

				💭 Act on pet
				↪️ action ➡️ the_action 🍇
					💭 Pet interaction

					↪️ 🍏🐕 the_action❓ 🍇

						💭 Feed pet
						🍕🐇🗺 the_action ❗
					🍉
					🙅↪️ 🥏🐕 the_action❓ 🍇

						💭 Play with pet
						🥏🐇🗺 the_action ❗
					🍉
					🙅↪️ 💊🐕 the_action❓ 🍇

						💭 Cure pet
						💊🐇🗺 the_action ❗
					🍉
					🙅↪️ 🧼🐕 the_action❓ 🍇

						💭 Clean pet
						🧼🐇🗺 the_action ❗
					🍉
					🙅↪️ the_action 🙌 🔤👋🔤 👐 the_action 🙌 🔤status🔤 🍇

						💭 Display pet status
						📊🐇🗺❗️
					🍉
					🙅↪️ the_action 🙌 🔤🔍🔤 👐 the_action 🙌 🔤help🔤 🍇

						💭 Display help
						😀 🔤termanimal usage:🔤 ❗️
						😀 🔤    :food_emoji:     feed pet 🔤 ❗️
						😀 🔤    :activity_emoji: play with pet 🔤 ❗️
						😀 🔤    :cure_emoji:     cure pet 🔤 ❗️
						😀 🔤    :clean_emoji:    clean pet 🔤 ❗️
						😀 🔤    👋, status       display pet info 🔤 ❗️
						😀 🔤    🖊, adopt        adopt a pet 🔤 ❗️
						😀 🔤    🔍, help         display this 🔤 ❗️
					🍉
					🙅 🍇

						😀 🍪🧬 pet ❗️ 🔤 didn't understood `🔤 the_action 🔤`🔤🍪 ❗️
						😀 🔤Run `termanimal (🔍|help)` for a list of command🔤 ❗️
					🍉
				🍉
				🙅🍇
					💭 Prompt status
					↪️ 📽🐇📀❗️➡️ pet 🍇

						🔤🔤 ➡️ 🖍🆕 emotion

						↪️ 🌡 pet ❗️ 🙌 1 🍇
							🔤🌡🔤 ➡️ 🖍emotion
						🍉
						🙅↪️ 🍂 pet ❗️ 🙌 1 🍇
							🔤🍂🔤 ➡️ 🖍emotion
						🍉
						🙅↪️ 🍕 pet ❗️ 🙌 4 🤝 🥏 pet ❗️ 🙌 4  🍇
							🔤😋🔤 ➡️ 🖍emotion
						🍉
						🙅↪️ 🍕 pet ❗️ 🙌 0 🤝 🥏 pet ❗️ 🙌 0  🍇
							🔤😔🔤 ➡️ 🖍emotion
						🍉
						🙅↪️ 🍕 pet ❗️ 🙌 0 🍇
							🔤🥺🔤 ➡️ 🖍emotion
						🍉
						🙅↪️ 🥏 pet ❗️ 🙌 0 🍇
							🔤😠🔤 ➡️ 🖍emotion
						🍉

						👄 🍪🧬 pet ❗️ emotion 🍪❗️
					🍉
				🍉
			🍉
			🙅 🍇

				💭 Remove file
				🗑🐇📀❗️
				😀 🔤Pet info corrupted, please adopt again🔤 ❗️
			🍉
		🍉
		🙅🍇

			↪️ action ➡️ the_action 🍇

				↪️ the_action 🙌 🔤🖊🔤 👐 the_action 🙌 🔤adopt🔤 🍇

					💭 Adopt pet
					🏡🐇🗺❗️
				🍉
				🙅 🍇

					😀 🔤You have not adopted a pet yet, run `termanimal 🖊`🔤 ❗️
				🍉
			🍉
		🍉
	🍉

	💭 Check if a grapheme is food emoji
	❓🍏 str 🔡 ➡️ 👌 🍇
		↩️ ❎ 🤷‍♂️ 🙌  🔍 🍏🐇😀❗️ str❗️❗️
	🍉

	💭 Check if a grapheme is activity emoji
	❓🥏 str 🔡 ➡️ 👌 🍇
		↩️ ❎ 🤷‍♂️ 🙌  🔍 🥏🐇😀❗️ str❗️❗️
	🍉

	💭 Check if a grapheme is cure emoji
	❓💊 str 🔡 ➡️ 👌 🍇
		↩️ ❎ 🤷‍♂️ 🙌  🔍 💊🐇😀❗️ str❗️❗️
	🍉

	💭 Check if a grapheme is clean emoji
	❓🧼 str 🔡 ➡️ 👌 🍇
		↩️ ❎ 🤷‍♂️ 🙌  🔍 🧼🐇😀❗️ str❗️❗️
	🍉
🍉
