📜 🔤😀.emojic🔤
📜 🔤📀.emojic🔤
📜 🔤🐶.emojic🔤
📜 🔤🗺.emojic🔤

💭 Parse command line arguments
🐇🤖🍇

	💭 Store command line `action` (first parameter)
	🖍🆕 action 🍬🔡

	🆕 🍇🍉

	💭 Parse command line arguments (filtered program name)
	💭 Cannot be run in initializer (bug)
	❗️🎛 args 🍨🐚🔡🍆 🍇

		↪️ 🐔 args ❗️ 🙌 1 🍇
			🐽 args 0 ❗️ ➡️ 🖍action
		🍉
	🍉

	💭 Act in function of action
	❗️🎬 🍇

		↪️ action ➡️ the_action 🍇
			💭 Pet interaction

			↪️ 🍏🐕 the_action❓ 🍇

				💭 Feed pet
				🍕🐇🗺 the_action ❗
			🍉
			🙅↪️ 🥏🐕 the_action❓ 🍇

				💭 Play with pet
				🥏🐇🗺 the_action ❗
			🍉
			🙅↪️ the_action 🙌 🔤status🔤 🍇

				💭 Display pet status
				📊🐇🗺❗️
			🍉
			🙅↪️ the_action 🙌 🔤adopt🔤 🍇

				💭 Adopt pet
				🏡🐇🗺❗️
			🍉
			🙅 🍇

				😀 🍪🔤Your pet didn't understand `🔤 the_action 🔤`.🔤🍪 ❗️
			🍉
		🍉
		🙅🍇
			💭 Prompt status
			↪️ 📽🐇📀❗️➡️ pet 🍇
				👄 🧬pet ❗️❗️
			🍉
		🍉
	🍉

	💭 Check if a grapheme is food emoji
	❓🍏 str 🔡 ➡️ 👌 🍇
		↩️ ❎ 🤷‍♂️ 🙌  🔍 🍏🐇😀❗️ str❗️❗️
	🍉

	💭 Check if a grapheme is activity emoji
	❓🥏 str 🔡 ➡️ 👌 🍇
		↩️ ❎ 🤷‍♂️ 🙌  🔍 🥏🐇😀❗️ str❗️❗️
	🍉
🍉
