📜 🔤😀.emojic🔤
📜 🔤📀.emojic🔤
📜 🔤🐶.emojic🔤
📜 🔤🗺.emojic🔤
📜 🔤⌚️.emojic🔤

💭 Parse command line arguments
🐇🤖🍇

	💭 Store command line `action` (first parameter)
	🖍🆕 action 🍬🔡

	🆕 🍇🍉

	💭 Parse command line arguments (filtered program name)
	💭 Cannot be run in initializer (bug)
	❗️🎛 args 🍨🐚🔡🍆 🍇

		↪️ 🐔 args ❗️ 🙌 1 🍇
			🐽 args 0 ❗️ ➡️ 🖍action
		🍉
	🍉

	💭 Act in function of action
	❗️🎬 🍇

		↪️ 🤔🐇📀❗️ 🍇

			💭 Pet file exists
			↪️ 📽🐇📀❗️➡️ pet 🍇

				💭 First of all, update pet status
				📯🐇⌚️ pet ❗️

				💭 Act on pet
				↪️ action ➡️ the_action 🍇
					💭 Pet interaction

					↪️ 🍏🐕 the_action❓ 🍇

						💭 Feed pet
						🍕🐇🗺 the_action ❗
					🍉
					🙅↪️ 🥏🐕 the_action❓ 🍇

						💭 Play with pet
						🥏🐇🗺 the_action ❗
					🍉
					🙅↪️ the_action 🙌 🔤👋🔤 👐 the_action 🙌 🔤status🔤 🍇

						💭 Display pet status
						📊🐇🗺❗️
					🍉
					🙅 🍇

						😀 🍪🔤Your pet didn't understand `🔤 the_action 🔤`🔤🍪 ❗️
					🍉
				🍉
				🙅🍇
					💭 Prompt status
					↪️ 📽🐇📀❗️➡️ pet 🍇

						🔤🔤 ➡️ 🖍🆕 emotion

						↪️ 🍕 pet ❗️ 🙌 4 🤝 🥏 pet ❗️ 🙌 4  🍇
							🔤😋🔤 ➡️ 🖍emotion
						🍉
						🙅↪️ 🍕 pet ❗️ 🙌 0 🤝 🥏 pet ❗️ 🙌 0  🍇
							🔤😔🔤 ➡️ 🖍emotion
						🍉
						🙅↪️ 🍕 pet ❗️ 🙌 0 🍇
							🔤🥺🔤 ➡️ 🖍emotion
						🍉
						🙅↪️ 🥏 pet ❗️ 🙌 0 🍇
							🔤😠🔤 ➡️ 🖍emotion
						🍉

						👄 🍪🧬 pet ❗️ emotion 🍪❗️
					🍉
				🍉
			🍉
			🙅 🍇

				💭 Remove file
				🗑🐇📀❗️
				😀 🔤Pet info corrupted, please adopt again🔤 ❗️
			🍉
		🍉
		🙅🍇

			↪️ action ➡️ the_action 🍇

				↪️ the_action 🙌 🔤🖊🔤 👐 the_action 🙌 🔤adopt🔤 🍇

					💭 Adopt pet
					🏡🐇🗺❗️
				🍉
				🙅 🍇

					😀 🔤You have not adopted a pet yet, run `termanimal 🖊`🔤 ❗️
				🍉
			🍉
		🍉
	🍉

	💭 Check if a grapheme is food emoji
	❓🍏 str 🔡 ➡️ 👌 🍇
		↩️ ❎ 🤷‍♂️ 🙌  🔍 🍏🐇😀❗️ str❗️❗️
	🍉

	💭 Check if a grapheme is activity emoji
	❓🥏 str 🔡 ➡️ 👌 🍇
		↩️ ❎ 🤷‍♂️ 🙌  🔍 🥏🐇😀❗️ str❗️❗️
	🍉
🍉
